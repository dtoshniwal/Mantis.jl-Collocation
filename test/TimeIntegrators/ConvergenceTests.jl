module TimeIntegrationConvergenceTests

using Mantis
using Test
import LinearAlgebra
import StaticArrays

# We can check the correctness of the implemented schemes by computing their rate of
# convergence and checking if this matches the expected rate. Here, we use a simple ODE
# with one unknown.
#
# Consider the ODE:
# dy/dt = lambda y,  y(t=0) = 1.0
# which has exact solution y(t) = exp(lambda t).

const lambda = -4
const t_final = 1

y_0 = 1.0
function exact_sol(t)
    return exp(lambda * t)
end

# Fully explicit ---------------------------------------------------------------------------
function test_ode_explicit_func(yn, t)
    return lambda * yn
end
test_ode_explicit = TimeIntegrators.define_explicit_ode(test_ode_explicit_func)

function implicitSolve(x, h, t)
    return (LinearAlgebra.I - h * lambda) \ x
end
test_ode_implicit = TimeIntegrators.define_implicit_ode(implicitSolve, x -> lambda * x)

test_ode_imex = TimeIntegrators.define_imex_ode(
    (yn, t) -> 0.5 * lambda * yn,  # Explcit evaluation
    (x, h, t) -> (LinearAlgebra.I - 0.5 * h * lambda) \ x,  # Implicit solver
    x -> 0.5 * lambda * x,  # Implicit evaluate
)

const explicit_integrators = (
    # Single-step, multi-stage
    TimeIntegrators.FORWARD_EULER,
    TimeIntegrators.EXPLICIT_MIDPOINT,
    TimeIntegrators.HEUN2,
    TimeIntegrators.RALSTON2,
    TimeIntegrators.HEUN3,
    TimeIntegrators.RK3,
    TimeIntegrators.RALTSON3,
    TimeIntegrators.VDHW3,
    TimeIntegrators.SSPRK3,
    TimeIntegrators.RK4,
    TimeIntegrators.RK4_3_8,
    TimeIntegrators.RALTSON4,
)

@testset "Single-Step Multi-Stage Explicit Integrators" verbose = true begin
    foreach(explicit_integrators) do scheme
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            dt = dt / 2
            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_explicit, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end
        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        @test isapprox(rates[end], TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

const explicit_multi_step_integrators = (
    # Multi-step, single-stage
    (TimeIntegrators.AB1, nothing),
    (TimeIntegrators.AB2, TimeIntegrators.HEUN2),
    (TimeIntegrators.AB3, TimeIntegrators.HEUN3),
    (TimeIntegrators.AB4, TimeIntegrators.RK4),
)
@testset "Multi-Step Single-Stage Explicit Integrators" verbose = true begin
    foreach(explicit_multi_step_integrators) do (scheme, startup_scheme)
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            if !isnothing(startup_scheme)
                y_n = TimeIntegrators.initializeScheme([y_0], scheme, startup_scheme)
            else
                y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            end
            dt = dt / 2
            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_explicit, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end
        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        @test isapprox(rates[end], TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

# Fully implicit ---------------------------------------------------------------------------
const implicit_integrators = (
    # Single-step, multi-stage
    TimeIntegrators.BACKWARD_EULER,
    TimeIntegrators.RADAU_IA_1,
    TimeIntegrators.IMPLICIT_MIDPOINT,
    TimeIntegrators.DIRK2,
    TimeIntegrators.RADAU_IA_3,
    TimeIntegrators.DIRK3,
    TimeIntegrators.DIRK4,
    TimeIntegrators.GAUSS_LEGENDRE_4,
    TimeIntegrators.GAUSS_LEGENDRE_6,
)

@testset "Single-Step Multi-Stage Implicit Integrators" verbose = true begin
    foreach(implicit_integrators) do scheme
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            dt = dt / 2
            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_implicit, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end
        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        if TimeIntegrators.get_order(scheme) > 4
            # For high-order methods, we reach machine precision so the rate bottoms out.
            # We can pick an earlier rate to check correctness.
            test_rate = rates[3]
        else
            test_rate = rates[end]
        end

        @test isapprox(test_rate, TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

# The BDF schemes also test the initialisation of schemes that only require previous
# solutions, but not previous stage derivatives. The AM schemes require implicit stage
# derivatives, but no additional previous solutions. The AB schemes require explicit stage
# derivatives, but no additional previous solutions.
const implicit_multi_step_integrators = (
    # Multi-step, single-stage
    (TimeIntegrators.AM0, nothing),
    (TimeIntegrators.AM1, TimeIntegrators.BACKWARD_EULER),
    (TimeIntegrators.AM2, TimeIntegrators.DIRK2),
    (TimeIntegrators.AM3, TimeIntegrators.DIRK3),
    (TimeIntegrators.AM4, TimeIntegrators.GAUSS_LEGENDRE_6),
    (TimeIntegrators.BDF1, nothing),
    (TimeIntegrators.BDF2, TimeIntegrators.BACKWARD_EULER),
    (TimeIntegrators.BDF3, TimeIntegrators.DIRK2),
    (TimeIntegrators.BDF4, TimeIntegrators.DIRK3),
)
@testset "Multi-Step Single-Stage Implicit Integrators" verbose = true begin
    foreach(implicit_multi_step_integrators) do (scheme, startup_scheme)
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            dt = dt / 2
            if !isnothing(startup_scheme)
                y_n = TimeIntegrators.initializeScheme([y_0], scheme, startup_scheme)
            else
                y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            end

            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_implicit, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end
        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        if TimeIntegrators.get_order(scheme) > 4
            # For high-order methods, we reach machine precision so the rate bottoms out.
            # We can pick an earlier rate to check correctness.
            test_rate = rates[4]
        else
            test_rate = rates[end]
        end
        @test isapprox(test_rate, TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

# IMEX -------------------------------------------------------------------------------------
const one_step_imex_integrators = (
    # Single-step, multi-stage
    TimeIntegrators.BACKWARD_FORWARD_EULER,
    TimeIntegrators.MIDPOINT_IMEX,
    TimeIntegrators.RK3_IMEX,
)

@testset "Single-Step Multi-Stage IMEX Integrators" verbose = true begin
    foreach(one_step_imex_integrators) do scheme
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            dt = dt / 2
            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_imex, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end

        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        @test isapprox(rates[end], TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

const multi_step_imex_integrators = (
    # Multi-step, single-stage
    (TimeIntegrators.CNAB2, TimeIntegrators.MIDPOINT_IMEX),
    (TimeIntegrators.SSSS2, TimeIntegrators.MIDPOINT_IMEX),
)

@testset "Multi-Step Single-Stage IMEX Integrators" verbose = true begin
    foreach(multi_step_imex_integrators) do (scheme, startup_scheme)
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            if !isnothing(startup_scheme)
                y_n = TimeIntegrators.initializeScheme([y_0], scheme, startup_scheme)
            else
                y_n = TimeIntegrators.initializeScheme([y_0], scheme)
            end

            dt = dt / 2
            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_imex, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end

        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        @test isapprox(rates[end], TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

# Combined multi-step multi-stage ----------------------------------------------------------

# Almost Runge-Kutta methods----------------------------------------------------------------
# Rattenbury, N., 2005. Almost Runge–Kutta methods for stiff  and non-stiff problems.
# Thesis (PhD). The University of Auckland.
# This scheme requires a specialised initialisation, since it needs an estimate of the
# second derivative which is not accounted for in the available initialisations. As a
# result, this scheme is not part of Mantis.
const ARK3 = TimeIntegrators.Explicit(
    StaticArrays.SMatrix{3, 3}(0.0, 1/2, 0.0, 0.0, 0.0, 3/4, 0.0, 0.0, 0.0), # A
    StaticArrays.SMatrix{3, 3}(0.0, 0.0, 3.0, 3/4, 0.0, -3.0, 0.0, 1.0, 2.0), # B
    StaticArrays.SMatrix{3, 3}(1.0, 1.0, 1.0, 1/3, 1/6, 1/4, 1/18, 1/18, 0.0), # U
    StaticArrays.SMatrix{3, 3}(1.0, 0.0, 0.0, 1/4, 0, -2.0, 0.0, 0.0, 0.0), # V
    StaticArrays.SVector(1 / 3, 2 / 3, 1.0),
    TimeIntegrators.TimeLevels(
        [0], # y
        Int[], # Δt G
        [0, 1],  # Δt F and Δt^2 F'
    ),
    3,
)
const explicit_multi_multi_integrators = (ARK3,)
@testset "Multi-Step Multi-Stage Explicit Integrators" verbose = true begin
    foreach(explicit_multi_multi_integrators) do scheme
        errors = zeros(8)
        dts = zeros(length(errors))
        dt = 0.2
        for i in eachindex(errors)
            dt = dt / 2
            yn = zeros(Float64, 1, 3)
            yn[:, 1] .= [y_0]
            yn[:, 2] .= lambda .* [y_0] .* dt
            yn[:, 3] .= lambda^2 .* [y_0] .* dt^2
            y_n = TimeIntegrators.TimeIntegrationSolution(yn, scheme, nothing, 0)

            dts[i] = dt
            for t in 0.0:dt:(t_final - dt)
                TimeIntegrators.timeIntegrate!(y_n, test_ode_explicit, t, dt)
            end

            errors[i] = abs(exact_sol(t_final) - TimeIntegrators.get_solution(y_n)[1])
        end
        rates = [
            log(errors[i]/errors[i + 1])/(log(dts[i]/dts[i + 1])) for
            i in eachindex(errors)[1:(end - 1)]
        ]

        # The rate is computed to 2 decimal places.
        @test isapprox(rates[end], TimeIntegrators.get_order(scheme), rtol=1e-2)
    end
end

end
